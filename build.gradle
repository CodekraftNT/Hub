/*
 * Build Script For SimpleHub, The Hub Plugin For Codekraft
 */

plugins {
	id 'java'
	id'eclipse'
	id 'com.github.johnrengelman.shadow' version '1.2.1'
	id "com.github.hierynomus.license" version "0.11.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0'
jar {
	manifest {
		attributes 'Implementation-Title': 'Simple Hub',
		'Implementation-Version': version,
		'Test-Entry': 'PASSED'
	}
}


repositories {
	mavenCentral()
	maven {
		name = "spigot-repo"
		url = "https://hub.spigotmc.org/nexus/content/groups/public/"
	}

	maven {
		name = "sonotype"
		url = "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	maven {
		name = "sonotype-public"
		url = "https://oss.sonatype.org/content/repositories/public/"
	}

	maven {
		name = "gravitydev"
		url = 'http://repo.gravitydevelopment.net'
	}

	maven {
		name = "mcstats"
		url = 'http://repo.mcstats.org/content/repositories/public'
	}

	maven {
		name = "bukkit-repo"
		url = "http://repo.bukkit.org/content/groups/public/"
	}

	maven {
		name = "shadowvolt-repo"
		url = "http://ci.shadowvolt.com/plugin/repository/everything/"
	}

	flatDir name: "lib", dirs: "${rootProject.projectDir}/lib"
}

dependencies {
	compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
	compile name: "spigot-api", group: "org.spigotmc", version: "1.8.7-R0.1-SNAPSHOT"
	compile name: "bungeecord-chat", group: "net.md-5", version: "1.8-SNAPSHOT"
	compile name: "bungeecord-api", group: "net.md-5", version: "1.8-SNAPSHOT"
	compile name: "snakeyaml", group: "org.yaml", version: "1.16-SNAPSHOT"
	compile name: "updater", group: "net.gravitydevelopment.updater", version: "2.1"
	compile name: "metrics", group: "org.mcstats.bukkit", version: "R8-SNAPSHOT"
	//compile name: "ProtocolLib", group: "com.comphenix.protocol", version: "3.6.3-SNAPSHOT"
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

license {
	header file('LICENSE')
	excludes([
		'**/*.MD',
		'**/*.properties',
		'**/*.yml'
	])
	sourceSets = project.sourceSets
	ignoreFailures false
	strictCheck true
	mapping { java = 'SLASHSTAR_STYLE' }
}

test { systemProperties 'property': 'value' }

uploadArchives {
	repositories { flatDir { dirs 'repos'
		} }
}

shadowJar {
	dependencies {
		exclude(dependency ('org.bukkit:bukkit'))
	}

	relocate 'org.mcstats', 'net.codekrafter.plugins.simplehub.mcstats'
	relocate 'net.gravitydevelopment.updater', 'net.codekrafter.plugins.simplehub.updater'
	relocate 'net.md_5.bungee.api', 'net.codekrafter.lib.bungee.api'

	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
	exclude 'license/*'
}

task push(type: Copy, dependsOn: 'build') {
	from("build/libs") { include '*.jar' }
	into "/Users/edds/Desktop/Servers/newServer/plugins"
}

task wrapper(type: Wrapper) { gradleVersion = "2.4" }
